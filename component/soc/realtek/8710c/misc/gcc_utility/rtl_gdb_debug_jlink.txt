# GDB script debug

#===============================================================================
#set GDB connection 
set remotetimeout 100000
target remote :2331

#===============================================================================
#Message display setting
#disable all messages

#===============================================================================
#Load flashloader
monitor reset 1
monitor sleep 20
monitor clrbp

#===============================================================================
#not a version, then will use wdt reset to reset peripherals and cpu will park at ram start
set $VER = (({int}0x400001F0) >> 4) & 0xF
printf "VER: %x\n",$VER
if ($VER != 0x0)
	printf "WDT RESET\n"
	set $REASON = ({int}0x400000F4) | 0x4
	set {int}0x400000F4 = $REASON
	set {int}0x10000480=0
	set {int}0x40002800 = 0x42A50001
	monitor sleep 2000
	monitor reset 1
	monitor sleep 20
	monitor clrbp
	set $REASON = $REASON & ~0x4
	set {int}0x400000F4 = $REASON
end

file ../../../component/soc/realtek/8710c/misc/iar_utility/flashloader/FlashRTL8710c.out
#Load the file
lo
printf "Load flash controller.\n"
#reset
monitor reset 3
monitor halt
#set break at FlashBreak
b FlashBreak
#set PC and SP
set $INIT_SP = {int}&__vector_table
set $INIT_PC = {int}(&__vector_table+4)
#set VTOR
set {int}0xe000ed08 = &__vector_table
#fast boot off
set {int}0x10000480 = &gRamStartFun
printf "INIT_SP = %x INIT_PC = %x\n", $INIT_SP, $INIT_PC
set $sp = $INIT_SP
set $pc = $INIT_PC
set {int}(&theFlashParams.base_ptr) = 0x98000000
set {int}(&theFlashParams.block_size) = 0x1000
set {int}(&theFlashParams.offset_into_block) = 0
set {int}(&theFlashParams.count) = 0
set {int}(&theFlashParams.buffer) = &FlashBufferStart
# continue
continue
printf "Flash Init done\n"
clear FlashBreak

#===============================================================================
set {int}0x10000480=0
#set {int}0x40002800=0x42A50001
monitor sleep 20
monitor reset 3
monitor halt
monitor sleep 20

#===============================================================================
#Load flash download file
source debug.gdb

hbreak main
continue
clear main





